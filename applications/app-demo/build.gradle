plugins {
    id "org.asciidoctor.jvm.convert" version "3.2.0"
}

configurations {
    asciidoctorExt
}

dependencies {
    implementation project(':components:module-demo')
    implementation project(':components:module-oauth')
    implementation project(':components:support-security')
    implementation project(':components:module-member')
    implementation project(':components:module-folder')
    implementation project(':components:module-posts')
    implementation project(':components:module-post-folder')
    implementation project(':components:support-jwt')
    implementation project(':components:support-jpa')
    implementation project(':components:component-category')

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    implementation 'com.github.zkdlu:api-response-spring-boot-starter:1.0.3'

    runtimeOnly 'com.h2database:h2'

    runtimeOnly 'mysql:mysql-connector-java'
    implementation 'mysql:mysql-connector-java'
}

ext {
    snippetDir = file('build/generated-snippets')
}

test {
    useJUnitPlatform()
    outputs.dir snippetDir
}

asciidoctor {
    configurations 'asciidoctorExt'
    inputs.dir snippetDir
    dependsOn test
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc/")
    into file("src/main/resources/static/docs")
}

bootJar {
    dependsOn copyDocument
}

bootRun {
    systemProperty "kakaoClientId", findProperty("kakaoClientId")
    systemProperty "kakaoRedirectUrl", findProperty("kakaoRedirectUrl")
}